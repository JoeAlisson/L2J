plugins {
    id "java"
    id "idea"
    id "eclipse"
}

ext.moduleName = 'com.l2jbr.gameserver'

sourceSets {
    main {
        java {
            srcDirs = ['src/main/com.l2jbr.gameserver']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}



configurations.all {
    transitive = false
}

dependencies {
    compile project(':Commons')
    compile project(':Mmocore')

    compile 'commons-logging:commons-logging:1.2'
    compile 'bsf:bsf:2.4.0'
    compile 'org.python:jython-standalone:2.7.1'

    runtime 'org.beanshell:bsh:2.0b5'
    runtime 'com.mchange:c3p0:0.9.5.2'
    runtime 'com.mchange:mchange-commons-java:0.2.15'
    runtime 'mysql:mysql-connector-java:8.0.9-rc'
}

def generalManifest = manifest {
    attributes('Built-By': System.getProperty('user.name'),
            'Built-Date': new Date().format("yyyy-MM-dd HH:mm:ss"))
}

jar {
    baseName 'l2j-gameserver'

    exclude('config/**')
    exclude('*.cfg')
    manifest {
        from(generalManifest)
        attributes('Main-Class': 'com.l2jbr.gameserver.GameServer')
    }
}

task zip(type: Zip, group: "Archive", description: "Zip Gameserver") {
    into('lib') {
        from([jar, configurations.runtime])
    }

    into('data') {
        from('data')
    }

    from('bin')
    from(sourceSets.main.resources)

    destinationDir project.buildDir
}

build.finalizedBy(zip)

