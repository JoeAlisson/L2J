plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "application"
}

ext.moduleName = 'com.l2jbr.gameserver'
mainClassName = "com.l2jbr.gameserver.GameServer"

version '1.3.1'

sourceSets {
    main {
        java {
            srcDirs = ['src/main/com.l2jbr.gameserver']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

dependencies {
    compile project(':Commons')
    compile project(':Mmocore')

    compile 'org.slf4j:slf4j-api:1.8.0-beta2'
    compile 'org.python:jython-standalone:2.7.1'
    compile 'org.springframework.data:spring-data-jdbc:1.0.0.M3'
    runtime 'org.apache.logging.log4j:log4j-core:2.11.0'
    runtime 'org.apache.logging.log4j:log4j-api:2.11.0'
    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.0'
    runtime 'org.beanshell:bsh:2.0b5'
    runtime 'com.zaxxer:HikariCP:3.1.0'
    runtime 'javax.xml.bind:jaxb-api:2.3.0'
    runtime 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    runtime 'com.sun.xml.bind:jaxb-impl:2.3.0.1'
    runtime 'javax.activation:activation:1.1.1'
    runtime ('mysql:mysql-connector-java:8.0.11') {
        transitive = false
    }

}

def generalManifest = manifest {
    attributes('Built-By': System.getProperty('user.name'),
            'Built-Date': new Date().format("yyyy-MM-dd HH:mm:ss"))
}

jar {
    baseName 'l2j-gameserver'

    exclude('config/**')
    manifest {
        from(generalManifest)
        attributes('Main-Class': 'com.l2jbr.gameserver.GameServer')
    }
}

task zip(type: Zip, group: "Archive", description: "Zip Gameserver") {
    into('lib') {
        from([jar, configurations.runtime])
    }

    into('data') {
        from('data')
    }

    from('bin')

    from(sourceSets.main.resources) {
        exclude('gameserver-messages*properties')
    }

    destinationDir project.buildDir
}

build.finalizedBy(zip)

